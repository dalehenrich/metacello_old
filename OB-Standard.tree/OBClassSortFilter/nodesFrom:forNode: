
!OBClassSortFilter methodsFor: 'filtering' stamp: 'lr 11/17/2010 16:04'!
nodesFrom: nodes forNode: parent 
	"Sort nodes according to their position in the class hierarchy"

	| classes supersChain |
	self initializeResults.
	classes := nodes collect: [ :classNode | classNode theClass ].
	classes isEmpty ifTrue: [ ^ nodes ].
	classes := classes size < 10
		ifTrue: [ classes asArray ]
		ifFalse: [ classes asSet ].
	nodes do: [ :classNode | 
		supersChain := classNode theClass withAllSuperclasses reversed.
		supersChain removeAllSuchThat: [ :class | (classes includes: class) not ].
		order add: supersChain contents -> classNode ].
	self buildIndentIndex.
	^ self collectSortedNodes! !
